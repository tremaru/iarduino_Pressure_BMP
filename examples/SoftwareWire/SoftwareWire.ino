// ПРИМЕР ВЫВОДА ДАВЛЕНИЯ, ТЕМПЕРАТУРЫ И ВЫСОТЫ                                     //
// Требуется установить библиотеку <iarduino_I2C_Software.h>                        // https://iarduino.ru/file/627.html
                                                                                    //
// Барометр BMP180 (Trema-модуль):                                                  // https://iarduino.ru/shop/Sensory-Datchiki/barometr-gy-68-datchik-atmosfernogo-davleniya-bmp180.html
// Барометр BMP280 (Trema-модуль):                                                  // https://iarduino.ru/shop/Sensory-Datchiki/barometr-trema-modul.html
// Информация о подключении модулей к шине I2C:                                     // https://wiki.iarduino.ru/page/i2c_connection/
// Информация о модуле и описание библиотеки:                                       // https://wiki.iarduino.ru/page/trema-modul-pressure-meter
                                                                                    //
#include <iarduino_I2C_Software.h>                                                  // Подключаем библиотеку для работы с программной шиной I2C, до подключения библиотеки iarduino_I2C_Bumper.
SoftTwoWire sWire(3,4);                                                             // Создаём объект программной шины I2C указав выводы которым будет назначена роль линий: SDA, SCL.
                                                                                    //
#include <iarduino_Pressure_BMP.h>                                                  // Подключаем библиотеку iarduino_Pressure_BMP для работы с датчиками атмосферного давления BMP180 или BMP280.
iarduino_Pressure_BMP sensor;                                                       // Объявляем объект sensor для работы с датчиком давления, используя функции и методы библиотеки iarduino_Pressure_BMP.
                                                                                    // Адрес модуля на шине I2C можно указать при объявлении объекта: iarduino_Pressure_BMP sensor(0x76); // По умолчанию 0x77.
void setup(){                                                                       //
     Serial.begin(9600);                                                            // Инициируем передачу данных в монитор последовательного порта на скорости 9600 бод.
     delay(1000);                                                                   // Ждём завершения переходных процессов при подаче питания
     sensor.begin(&sWire);                                                          // Инициируем работу с датчиком, указав ссылку на объект для работы с шиной I2C на которой находится датчик (по умолчанию &Wire).
}                                                                                   // Объект основной аппаратной шины I2C (Wire) можно не указывать: sensor.begin();
                                                                                    //
void loop(){                                                                        //
//   Считываем давление в мм.рт.ст., температуру и высоту:                          //
     if( sensor.read(1) ){                                                          // Функция read(1) читает давление в мм.рт.ст., температуру в °С и высоту в м.
         Serial.print((String) "Датчик BMP"+ sensor.type        + ": ");            //
         Serial.print((String) "P = "      + sensor.pressure    + " мм.рт.ст, \t"); //
         Serial.print((String) "T = "      + sensor.temperature + " *C, \t\t");     //
         Serial.print((String) "B = "      + sensor.altitude    + " м.\r\n");       //
     }else{                                                                         //
         Serial.println("Нет ответа от датчика.");                                  //
     }                                                                              //
                                                                                    //
//   Считываем давление в Па, температуру и высоту:                                 //
     if( sensor.read(2) ){                                                          // Функция read(2) читает давление в Па, температуру в °С и высоту в м.
         Serial.print((String) "Датчик BMP"+ sensor.type        + ": ");            //
         Serial.print((String) "P = "      + sensor.pressure    + " Pa, \t");       //
         Serial.print((String) "T = "      + sensor.temperature + " *C, \t\t");     //
         Serial.print((String) "B = "      + sensor.altitude    + " м.\r\n");       //
     }else{                                                                         //
         Serial.println("Нет ответа от датчика.");                                  //
     }                                                                              //
                                                                                    //
     delay(3000);                                                                   //
}                                                                                   //

// Библиотека iarduino_Pressure_BMP для работы датчиками атмосферного давления BMP180 и BMP280.
// Библиотека имеет всего 3 функции: begin(), read(), measurement(), три переменные типа float: temperature, pressure, altitude и две переменные типа int: type, version.
// ========================
// Функция begin();        Предназначена для инициализации датчика и может принимать в качестве необязательного параметра значение начальной высоты (по умолчанию 0).
//                         Высота указывается в метрах, значение типа float. Можно указать высоту над уровнем моря. От этого значения рассчитывается высота в переменной altitude.
//                         Например: если вызвать функцию begin(10), подняться на 15 метров и вызвать функцию read(), то в переменной altitude будет значение 25 (10м + 15м).
//                         Функция возвращает значение типа bool (true или false) сообщающее о результате инициализации модуля.
//                         Функцию можно вызвать однократно (в коде setup), а можно и многократно (для корректировки или изменения начальной высоты).
// ========================
// Функция read();         Инициирует чтение (вычисление) текущих значений: температуры окружающей среды, атмосферного давления и высоты относительно начальной (указанной в функции begin()).
//                         Функция принимает одну необязательную цифру: 1 или 2 (по умолчанию 1). Если 1 то давление рассчитывается в мм.рт.ст., если 2 то давление рассчитывается в Па.
//                         Функция возвращает значение типа bool (true или false) сообщающее о результате чтения и вычисления данных.
// ========================
// Функция measurement();  Необязательная функция позволяющая выбрать точность измерений (уровень подавления шумов). Давление может «скакать» от ветра, хлопков или громкой музыки, это и есть шум показаний.
//                         Функция принимает одно обязательное, положительное, целочисленное значение от 0 (без подавления шума) до 3 (для bmp180) или до 4 (для bmp280) включительно. По умолчанию максимум.
//                         Функция возвращает значение типа bool (true или false) сообщающее о результате применения новой точности (значения передискретизации).
//                         Если Вам не нужна высокая точность измерений, её можно понизить, что приведёт к уменьшению энергопотребления, а при работе с датчиком bmp180 еще и к ускорению чтения данных.
// ========================
// Результаты:             Результаты измерений доступны для чтения из переменных: temperature, pressure, altitude, после вызова функции read(). Все переменные имеют тип float.
//                         temperature - хранит значение текущей температуры в °С, pressure - хранит текущее значение атмосферного давления в Па или мм. рт. ст., altitude - хранит высоту в метрах относительно начальной.
// ========================
// I2C адрес датчика:      У датчиков BMP180 фиксированный адрес на шине I2C = 0x77, а у датчиков BMP280 может быть установлен адрес 0x77 или 0x76. По умолчанию библиотека общается с датчиками по адресу 0x77.
//                         Адрес датчика (модуля) можно указать при объявлении объекта библиотеки: вместо iarduino_Pressure_BMP sensor; напишите: iarduino_Pressure_BMP sensor(0x76);
// ========================
// Тип и версия:           Тип и версию датчика можно прочитать из переменных type и version, после вызова функции begin(). Обе переменные имеют тип int.